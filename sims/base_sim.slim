initialize() {
			
	initializeSLiMModelType("nonWF");

	// DO SOME SCALING
	defineConstant('Q', 1000);
	
	// Command-line Arguments:
	//defineConstant('base_mut_rate', 5e-8);
	//defineConstant('store_dir', '/nas/longleaf/home/amjada/Desktop/eff_test/cancer_rescue/');
	//defineConstant('job_id', 1);
	//defineConstant('param_id', 1);
	//defineConstant('second_strike_lag', 0);
 	//defineConstant('cross_resistance', 0.5); 
	//defineConstant('dose_drug_1_mono', 0.9);
	//defineConstant('dose_drug_2_mono', 0.9);
	//defineConstant('dose_drug_1_combo', 0.6);
	//defineConstant('dose_drug_2_combo', 0.6);
	//if specifying mutation rate for each mutation type
	//defineConstant('mut_rate_m1', 1e-7);
	//defineConstant('mut_rate_m2', 1e-5);

	
	defineGlobal('dose_drug_1', 0);
	defineGlobal('dose_drug_2', 0);
	defineConstant('turn_over_rate', 0.20);


	defineConstant('expansion_time', 20);
	defineConstant('treatment_time', 300);
    
	defineConstant('mut_rate', base_mut_rate*Q);
	defineGlobal('fitness_scaling', 1-turn_over_rate);
	

	defineGlobal('kill_counter', 0);	
	defineGlobal('expansion_over', F);	
	
	defineGlobal('log_file', store_dir + '/log_' + asString(param_id) + '_' + asString(job_id) + '.csv');
	
	initializeMutationType("m1", 1.0, "f", 0);
	initializeMutationType("m2", 1.0, "f", 0);
	initializeMutationType("m3", 1.0, "f", 0);
	m1.mutationStackPolicy = "f";
	m2.mutationStackPolicy = "f";
	m3.mutationStackPolicy = "f";
	
	// Comment following 3 lines for different mutation rates for each mutation type
	initializeGenomicElementType("g1", c(m1, m2, m3), c(0.45, 0.45, 0.1));
	initializeGenomicElement(g1, 0, 0);
	initializeMutationRate(mut_rate);

	// Uncomment following 4 lines for different mutation rates for each mutation type
	//initializeGenomicElementType('g1', m1, 0, 0)
	//initializeGenomicElementType('g2', m2, 1, 1)
	//We divide by 2 since we have two genomic sites now instead of one
	//initializeMutationRate(c(mut_rate_m1/2 * Q, mut_rate_m2/2 * Q), c(0,1));
	
	initializeRecombinationRate(0);
}

reproduction(){    
	o1 = subpop.addCloned(individual);
}

fitnessEffect(p1){
	m1_i = asInteger(individual.countOfMutationsOfType(m1) > 0);
	m2_i = asInteger(individual.countOfMutationsOfType(m2) > 0);
	m3_i = asInteger(individual.countOfMutationsOfType(m3) > 0);
	
	fitness = (1 - dose_drug_1*(1-m1_i)*(1-cross_resistance*m3_i)) *
				 (1 - dose_drug_2*(1-m2_i)*(1-cross_resistance*m3_i)) 
				 
	
	return(fitness);

}

function (void)write_to_log(string$ sim_stage){
	
	neut_counts = 0;
	mut_counts = rep(0, length(sim.mutationTypes) + 1);

	if (p1.individualCount > 0){	
		for (ind in p1.individuals){
			both_muts = T;
			for (mut_type in sim.mutationTypes[0:(length(sim.mutationTypes)-1)]){
				if (ind.countOfMutationsOfType(mut_type) > 0){
					mut_counts[mut_type.id - 1] = mut_counts[mut_type.id - 1] + 1;
				} else {
					both_muts = F;
				}
			}

			if (both_muts){
				mut_counts[length(sim.mutationTypes)] = mut_counts[length(sim.mutationTypes)] + 1;
			}
			

		}	
	}
	
	mut_freqs = mut_counts / length(p1.individuals);
	
	//Write to file
	count_mat = c(param_id, job_id, sim.cycle, current_phase, mut_freqs, base_mut_rate, second_strike_lag,
	cross_resistance, dose_drug_1_combo, dose_drug_2_combo, dose_drug_1_mono, dose_drug_2_mono, expansion_time, treatment_time, current_drug, p1.individualCount, kill_counter, Q, job_id, sim_stage);
	to_write = paste(count_mat, sep=',');
	writeFile(log_file, to_write, append = T); 
	
}

function (float)find_m2_freq(void){
	
	count = 0;
	
	resistance_mut = sim.mutationTypes[current_drug - 1];
	for (ind in p1.individuals){
		if (ind.countOfMutationsOfType(resistance_mut) > 0){
			count = count + 1;
		}
	}
	
	
	final_freq = count / length(p1.individuals);
	
	return(final_freq);

}

function (void)check_population(void){
	if (length(p1.individuals) < (2^expansion_time - 0.10*(2^expansion_time))){
		community.registerEarlyEvent(NULL, '{check_population();}', sim.cycle + 1, sim.cycle + 1);
	} else {
		
		treatment_end_gen = sim.cycle + treatment_time;
		defineGlobal('current_phase', 'treatment');
		if (second_strike_lag == 0){
			defineGlobal('current_drug', 'both');
			defineGlobal('dose_drug_1', dose_drug_1_combo);
			defineGlobal('dose_drug_2', dose_drug_2_combo);
		} else {
			defineGlobal('dose_drug_1', dose_drug_1_mono);
			defineGlobal('current_drug', 1);
			community.registerEarlyEvent(NULL, '{defineGlobal("current_drug", 2);}', sim.cycle + asInteger(second_strike_lag));
			community.registerEarlyEvent(NULL, '{defineGlobal("dose_drug_1", 0);}', sim.cycle + asInteger(second_strike_lag));
			community.registerEarlyEvent(NULL, '{defineGlobal("dose_drug_2", dose_drug_2_mono);}', sim.cycle + asInteger(second_strike_lag));
		}
		p1.fitnessScaling = fitness_scaling;
		community.registerLateEvent(NULL, '{end_simulation();}', treatment_end_gen, treatment_end_gen);
	}

}

function (void)end_simulation(void){

	sim.simulationFinished();
	
}


1 early() {

	sim.addSubpop("p1", 1);
	community.registerEarlyEvent(NULL, "{check_population();}", sim.cycle + 1, sim.cycle + 1);
	log_headers = c('parm_id', 'job_id', 'cycle', 'phase', 'freq_m' + c(1:length(sim.mutationTypes)), 'freq_all_muts', 
    'base_mut_rate', 'second_treatment_lag', 'cross_resistance', 'drug_1_combo_dose', 'drug_2_combo_dose', 'drug_1_mono_dose', 'drug_2_mono_dose', 'expansion_time', 'treatment_time', 'current_drug', 'N', 'death_count', 'Q', 'replicate', 'sim_stage'); 
	
	if (!fileExists(log_file)){
		to_write = paste(log_headers, sep=',');
		writeFile(log_file, to_write);
	}

	num_muts = length(sim.mutationTypes);
	defineConstant("mut_count_mat", matrix(rep(0,num_muts), nrow=1));
	
	defineGlobal('current_phase', 'expansion');
	
}

early() {
	if (length(p1.individuals) > 1 | current_phase == 'treatment') {
		p1.fitnessScaling = (1-turn_over_rate);
	}
	write_to_log('early');
}

late() {
	write_to_log('late');
	if ((length(p1.individuals) > 2^(expansion_time)*1.5) | length(p1.individuals) == 0){
		end_simulation();
	}
}
